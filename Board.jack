class Board {
    field Matrix maze;
    field View view;
    field int playerX;
    field int playerY;
    field int playerDir;
    field PlayerMap playerMap;


    field int coinsCollected;
    field boolean inEnd;

    constructor Board new(Matrix aMaze, int startX, int startY){
        let maze = aMaze;
        let playerX = startX;
        let playerY = startY;
        let view = View.new();
        let playerMap = PlayerMap.new(maze, 0, 1);
        let playerDir = 1;
        let coinsCollected = 0;
        let inEnd = false;
        return this;
    }

    method void showMap() {
        do playerMap.display(playerX, playerY, playerDir);
        return;
    }
    
    //Moves the player by (x,y) blocks
    //x and y cannot be outside the range [-1,1]
    method void movePlayer(int x, int y) {
        var int newPosX;
        var int newPosY;
        if (playerDir = 0) {
            let newPosX = playerX + x;
            let newPosY = playerY + y;
        }
        if (playerDir = 1) {
            let newPosX = playerX + y;
            let newPosY = playerY - x;
        }
        if (playerDir = 2) {
            let newPosX = playerX - x;
            let newPosY = playerY - y;
        }
        if (playerDir = 3) {
            let newPosX = playerX - y;
            let newPosY = playerY + x;
        }

        if ((newPosX < 0) | (newPosX > (maze.getWidth() - 1)) | 
            (newPosY < 0) | (newPosY > (maze.getHeight() - 1)) |
            (maze.getInt(newPosX,newPosY) = 1)) {
            return;
        }
        if (maze.getInt(newPosX,newPosY) = 4) {
            let coinsCollected = coinsCollected + 1;
            do maze.setInt(newPosX,newPosY,0);
        }
        if (maze.getInt(newPosX,newPosY) = 3) {
            let inEnd = true;
        }
        
        let playerX = newPosX;
        let playerY = newPosY;
        return;
    }
    
    //Rotates the player
    //0 = facing up
    //1 = facing right
    //2 = facing down
    //3 = facing left
    method void rotatePlayer(boolean direction) {
        var int value;
        if (direction) {
            let playerDir = playerDir + 1;
            if (playerDir > 3) {
                let playerDir = 0;
            }
        }
        else {
            let playerDir = playerDir - 1;
            if (playerDir < 0) {
                let playerDir = 3;
            }
        }
        return;
    }

    method void updateView() {
        var Matrix viewMatrix;
        var int y;
        var int value;
        let viewMatrix = Matrix.new(3,6);
        let y = 0;
        while (y < 6) {
            let value = getViewSquare(0, y);
            do viewMatrix.setInt(0,y,value);
            
            //Mirroring left and right... still not sure why
            let value = getViewSquare(-1, y);
            do viewMatrix.setInt(2,y,value);

            let value = getViewSquare(1, y);
            do viewMatrix.setInt(1,y,value);
            
            let y = y + 1;
        }
        do view.setView(viewMatrix);
        do view.drawView();
        return;
    }

    method int getViewSquare(int x, int y) {
        var int newX;
        var int newY;
        var int mazeW;
        var int mazeH;
        var int value;

        if (playerDir = 0) {
            let newX = playerX + x;
            let newY = playerY + y;
        }
        if (playerDir = 1) {
            let newX = playerX + y;
            let newY = playerY - x;
        }
        if (playerDir = 2) {
            let newX = playerX - x;
            let newY = playerY - y;
        }
        if (playerDir = 3) {
            let newX = playerX - y;
            let newY = playerY + x;
        }

        let mazeW = maze.getWidth();
        let mazeH = maze.getHeight();
        if ((newX < 0) | (newX > (mazeW - 1)) | (newY < 0) | (newY > (mazeH - 1))) {
            return 1;
        }
        return maze.getInt(newX,newY);
    }

    method int getCoins() {
        return coinsCollected;
    }

    method boolean isInEnd() {
        return inEnd;
    }

    method void dispose() {
        do maze.dispose();
        do view.dispose();
        do playerMap.dispose();
        do Memory.deAlloc(this);
        return;
    }
}