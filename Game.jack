class Game {
    field int lastKey;
    field Board board;
    field boolean mapDisplayed;
    constructor Game new(int seed) {
        var Matrix maze;
        var Random random;
        let random = Random.new(seed);
        let maze = MazeBuilder.buildMaze(random); 
        do random.dispose();
        let board = Board.new(maze, 0, 15);
        do processFrame(1);
        return this;
    }

    method boolean gameLoop() {
        var int key;
        var int coins;
        let key = Keyboard.keyPressed();
        
        if (key > 96) { //To accept both uppercase and lowercase
            let key = key - 32;
        }
        //W = 87, A = 65, S = 83, D = 68
        //Q = 81, E = 69, M = 77
        if (~(key = lastKey)) {
            let lastKey = key;
            do processFrame(key);
        }
        if (board.isInEnd()) {
            let coins = board.getCoins();
            do endScreen(coins);
            return false;
        }
        return true;
    }

    method void processFrame(int key) {
        if (key = 0) {
            return;
        }

        //Mirroring left and right... still not sure why
        if (~mapDisplayed) {
            if ((key = 87)) { //W
                do board.movePlayer(0,1);
            }
            if (key = 65) { //A
                do board.movePlayer(1,0);
            }
            if (key = 83) { //S
                do board.movePlayer(0,-1);
            }
            if (key = 68) { //D
                do board.movePlayer(-1,0);
            }
            if (key = 81) { //Q
                do board.rotatePlayer(true);
            }
            if (key = 69) { //E
                do board.rotatePlayer(false);
            }
        }
        if (key = 77) {
            let mapDisplayed = ~mapDisplayed;
        }
        do Draw.clearScreen();
        do board.updateView();
        do Output.moveCursor(0,0);
        do Output.printString("Coins: ");
        do Output.printInt(board.getCoins());
        do Output.printString(" / 5");
        if (mapDisplayed) {
            do board.showMap();
        }
        return;
    }

    method void endScreen(int coins) {
        var int key;
        do Screen.setColor(false);
        do Screen.drawRectangle(0, 0, 511, 255);
        do Screen.setColor(true);
        do Output.moveCursor(8,25);
        do Output.printString("You Escaped!");
        do Output.moveCursor(12,20);
        do Output.printString("Coins collected: ");
        do Output.printInt(coins);
        do Output.printString(" / 5");
        do Output.moveCursor(20, 21);
        do Output.printString("Press [R] to Restart");
        while (~(Keyboard.keyPressed() = 82) & ~(Keyboard.keyPressed() = 114)) {}
        return;
    }

    method void dispose() {
        do board.dispose();
        do Memory.deAlloc(this);
        return;
    }
}