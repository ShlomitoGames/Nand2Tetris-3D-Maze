class PlayerMap {
  // information about each cell if ivisted or not
  // Visit map can have the following values:
  // 0 - unknown cell
  // 1 - unknown area
  // 2 - area that is known if it's occupied or non-occupied
  field Matrix visitMap;
  field int offsetX;
  field int offsetY;


  /// Creates a new maze map
  constructor PlayerMap new(Matrix maze, int aOffsetX, int aOffsetY) {
    var int width, height, x, y;
    let offsetX = aOffsetX;
    let offsetY = aOffsetY;
    let width = maze.getWidth();
    let height = maze.getHeight();
    let visitMap = Matrix.new(width, height);
    let y = 0;
    while (y < height) {
      let x = 0;
      while (x < width) {
        do visitMap.setInt(x, y, 0);
        let x = x + 1;
      }
      let y = y + 1;
    }
    return this;
  } 

  method void dispose() {
    do visitMap.dispose();
    do Memory.deAlloc(this);
    return;
  }

  function char getPlayerSymbol(int playerDir) {
    if (playerDir = 2) { // up
      return 94; // ^
    }
    if (playerDir = 1) { // right
      return 62; // >
    }
    if (playerDir = 0) {
      return 118; // v
    }
    if (playerDir = 3) {
      return 60; // <
    }
    // * is the unknown position symbol
    return 42; // *
  }

  method void visitArea(int left, int top, int w, int h, int value) {
    var int x, y, bottom, right, visitState;
    let right = Math.min(left + w, visitMap.getWidth());
    let bottom = Math.min(top + h, visitMap.getHeight());
    let y = Math.max(top, 0);
    while (y < bottom) {
      let x = Math.max(left, 0);
      while (x < right) {
        let visitState = visitMap.getInt(x, y);
        if (value > visitState) {
          do visitMap.setInt(x, y, value);
        }
        let x = x  + 1;
      }
      let y = y + 1;
    } 
    return;
  }

  /// Display the board on screen
  method void display(Matrix maze, int playerX, int playerY, int playerDir) {
    var int x, y, width, height, mask, visitState, mazeCell;
    var char cellChar;
    let width = visitMap.getWidth();
    let height = visitMap.getHeight();
    let y = 0;
    while (y < height) {
      do Output.moveCursor(offsetY + y, offsetX);
      let x = 0;
      while (x < width) { 
        let cellChar = 0;
        if ((playerX = x) & (playerY = y)) {
          let cellChar = PlayerMap.getPlayerSymbol(playerDir);
        } else {
          let visitState = visitMap.getInt(x, y);
          let mazeCell = maze.getInt(x, y);
          if (visitState = 0) {
            let cellChar = 32; // ' '
          }
          if (visitState = 1) {
            if (mazeCell = 1) { // Wall
              let cellChar = PlayerMap.getCellSymbol(1);
            } else { // Anything else
              let cellChar = PlayerMap.getCellSymbol(0);
            }
          }
          if (visitState = 2) {
            let cellChar = PlayerMap.getCellSymbol(maze.getInt(x, y));          
          }
        }
        do Output.printChar(cellChar);
        let x = x + 1;        
      }
      let y = y + 1;
      do Output.println();
    }
    return;
  }

  function char getCellSymbol(int cell) {
    if (cell = 0) {
      return 46;  // _ .
    }
    if (cell = 1) {
      return 35;  // #
    }
    if (cell = 2) {
      return 83; // S
    }
    if (cell = 3) {
      return 71; // G
    }
    if (cell = 4) {
      return 43; // +
    }
    return 63;
  }

  function void printDebugMaze(Matrix matrix) {
    var int x, y, width, height, cell;
    var char symbol;
    let width  = matrix.getWidth();
    let height  = matrix.getHeight();
    let y = 0;
    while (y < height) {
      let x = 0;
      while (x < width) {
        let cell = matrix.getInt(x, y);
        let symbol = PlayerMap.getCellSymbol(cell);
        do Output.printChar(symbol);
        let x = x + 1;
      }
      do Output.println();
      let y = y + 1;
    }
    return;
  }  
}