class PlayerMap {
  // information about each cell if ivisted or not
  field Matrix visitMap;
  field int offsetX;
  field int offsetY;

  /// Creates a new maze map
  constructor PlayerMap new(Matrix maze, int aOffsetX, int aOffsetY) {
    var int width, height, x, y;
    let offsetX = aOffsetX;
    let offsetY = aOffsetY;
    let width = maze.getWidth();
    let height = maze.getHeight();
    let visitMap = Matrix.new(width, height);
    let y = 0;
    while (y < height) {
      let x = 0;
      while (x < width) {
        do visitMap.setInt(x, y, 0);
        let x = x + 1;
      }
      let y = y + 1;
    }
    return this;
  } 

  method void dispose() {
    do visitMap.dispose();
    do Memory.deAlloc(this);
    return;
  }

  function char getPlayerSymbol(int playerDir) {
    if (playerDir = 0) { // up
      return 94; // ^
    }
    if (playerDir = 1) {
      return 62; // >
    }
    if (playerDir = 2) {
      return 118; // v
    }
    if (playerDir = 3) {
      return 60; // <
    }
    // * is the unknown position symbol
    return 42; // *
  }

  method void visit(int x, int y) {
    do visitMap.setInt(x, y, 1);
    return;
  }


  /// Display the board on screen
  method void display(Matrix maze, int playerX, int playerY, int playerDir) {
    var int x, y, width, height, mask;
    var char cellChar;
    let width = visitMap.getWidth();
    let height = visitMap.getHeight();
    let y = 0;
    while (y < height) {
      do Output.moveCursor(offsetY + y, offsetX);
      let x = 0;
      while (x < width) { 
        let cellChar = 0;
        if ((playerX = x) & (playerY = y)) {
          let cellChar = PlayerMap.getPlayerSymbol(playerDir);
        }
        if ((cellChar = 0)) {
          let mask = visitMap.getInt(x,y);
          if (mask = 0) {
            let cellChar = 32; // ' '
          }
        }
        if (cellChar = 0) {
          let cellChar = MazeBuilder.getCellSymbol(maze.getInt(x, y));
        }
        do Output.printChar(cellChar);
        let x = x + 1;        
      }
      let y = y + 1;
      do Output.println();
    }
    return;
  }
}